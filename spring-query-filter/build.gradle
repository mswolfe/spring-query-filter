group 'com.github.mswolfe'
version '4.3.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework:spring-framework-bom:4.3.2.RELEASE'
    compile 'org.springframework:spring-context:4.3.2.RELEASE'
    compile 'org.springframework:spring-web:4.3.2.RELEASE'
    compile 'org.springframework:spring-webmvc:4.3.2.RELEASE'

    compile 'commons-beanutils:commons-beanutils-core:1.8.3'
    compile 'javax.validation:validation-api:1.1.0.Final'

    testCompile 'org.codehaus.groovy:groovy-all:2.4.1'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testRuntime 'cglib:cglib-nodep:3.1'
    testRuntime 'org.objenesis:objenesis:2.1'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: project.findProperty('ossrhUsername') ?: '', password: project.findProperty('ossrhPassword') ?: '')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: project.findProperty('ossrhUsername') ?: '', password: project.findProperty('ossrhPassword') ?: '')
            }

            pom.project {
                name 'Spring Query Filter'
                packaging 'jar'
                description "A library that provides parsing for the 'filter' query parameter"
                url 'https://github.com/mswolfe/spring-query-filter'

                issueManagement {
                    url 'https://github.com/mswolfe/spring-query-filter/issues'
                    system 'GitHub Issues'
                }

                scm {
                    connection 'scm:git:git://github.com/mswolfe/spring-query-filter.git'
                    developerConnection 'scm:git:git@github.com:mswolfe/spring-query-filter.git'
                    url 'https://github.com/mswolfe/spring-query-filter'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/mswolfe/spring-query-filter/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'mswolfe'
                        name 'Michael Wolfe'
                        email 'mwooolfe@gmail.com'
                    }
                }
            }
        }
    }
}
